group = 'net.abesto.snakelike'
version = '0.0.1'

buildscript {
	ext.kotlinVersion = '0.1-SNAPSHOT'
	ext.kotlinGradlePluginVersion = '0.1-SNAPSHOT'
	ext.kotlinHome = "${System.env.HOME}/Library/Application Support/IdeaIC14/Kotlin"
	ext.kotlincJs = "$kotlinHome/kotlinc/bin/kotlinc-js"
	ext.kotlinJsLib = "$kotlinHome/kotlinc/lib/kotlin-jslib.jar"
	ext.kotlinJs = "$kotlinHome/kotlinc/lib/kotlin.js"

	repositories {
		mavenCentral()
		maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinGradlePluginVersion"
	}
}

task copyKotlinJs(type: Copy) {
	from "$kotlinJs"
	into "$rootDir/web/js/lib"
}

subprojects {
	apply plugin: 'kotlin'

	repositories {
		mavenCentral()
		maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
	}

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
	}
}

project(':logic') {
	dependencies {
		testCompile "junit:junit:4.11"
	}

	task jarSources(type:Jar){
		from sourceSets.main.allSource
		classifier = 'source'
	}
}

project(':ui') {
	ext.jsFile = "app"
	ext.output = {"$rootDir/web/js/app/${jsFile}.js"}
	ext.libraryFiles = [kotlinJsLib, project(':logic').tasks.jarSources.archivePath]

	dependencies {
		compile files(kotlinJsLib)
		compile project(':logic')
	}

	task build(overwrite: true) {
		dependsOn copyKotlinJs
		dependsOn ':logic:jarSources'

		ext.sources = sourceSets.main.allSource.files
		inputs.files sources

		outputs.file output()

		doLast {
			exec {
				commandLine("bash", kotlincJs, "-verbose", "-version",
						"-output", output(),
						"-library-files", libraryFiles.join(','), *sources.toList())
			}
		}
	}

	task test(overwrite: true) {
		logger.info "JS testing not implemented"
	}
}
