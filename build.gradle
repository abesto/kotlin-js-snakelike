apply plugin: 'kotlin'
group = 'net.abesto.snakelike.js.kotlin'
version = '0.0.1'

repositories {
	mavenCentral()
}

buildscript {
	ext.kotlinVersion = '0.8.484'
	ext.kotlinGradlePluginVersion = "0.8.484"
	ext.kotlinHome = "${System.env.HOME}/Library/Application Support/IdeaIC14/Kotlin"
	ext.kotlincJs = "$kotlinHome/kotlinc/bin/kotlinc-js"
	ext.kotlinJsLib = "$kotlinHome/kotlinc/lib/kotlin-jslib.jar"
	ext.kotlinJs = "$kotlinHome/kotlinc/lib/kotlin.js"

	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinGradlePluginVersion"
	}
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
	compile "junit:junit:4.11"
	compile files(kotlinJsLib)
}

sourceSets {
	main.java.srcDirs += 'src/main/kotlin'
	test.java.srcDirs += 'src/test/kotlin'
	mainJs {
		java {
			srcDir 'src/main/kotlin-js'
		}
	}
}

task copyKotlinJs(type: Copy) {
	from "$kotlinJs"
	into "$rootDir/web/js/lib"
}

task build(overwrite: true) {
	dependsOn copyKotlinJs

	ext.sources = sourceSets.mainJs.allSource.files + sourceSets.main.allSource.files
	inputs.files sources

	ext.output = "$rootDir/web/js/app/kotlin-js-snakelike.js"
	outputs.file output

	doLast {
		exec {
			commandLine("bash", kotlincJs, "-verbose", "-version",
					"-output", output, "-libraryFiles", kotlinJsLib, *sources.toList())
		}
	}
}
